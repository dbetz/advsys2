object location {
}

object actor {
}

property north, south, east, west;
property loc, description;

def connect(p, c)
{
  c.parent = p;
  c.sibling = p.child;
  p.child = c;
}

def disconnect(obj)
{
    var this = obj.parent.child;
    var prev = nil;
    while (this) {
        if (this == obj) {
            if (prev)
                prev.sibling = this.sibling;
            else
                this.parent.child = this.sibling;
            this.parent = nil;
            break;
        }
        prev = this;
        this = this.sibling;
    }
}

def printContents(obj, prop)
{
    var desc;
    obj = obj.child;
    while (obj) {
        if ((desc = obj.prop) != nil)
            print " ", #desc $;
        obj = obj.sibling;
    }
}

def listContents(obj, prop)
{
    var desc;
    obj = obj.child;
    while (obj) {
        if ((desc = obj.prop) != nil)
            print "\t", #desc;
        obj = obj.sibling;
    }
}

def getp(obj, prop)
{
    var value;
    try {
        value = obj.(prop);
    }
    catch (e) {
        value = 0;
    }
    return value;
}

def getc()
{
    asm {
        TRAP 0
            RETURN
    }
}

def screen(n)
{
    asm {
      LADDR 0
      LOAD
      TRAP 7
    }
}

var actors[] = { northActor, westActor, southActor, eastActor };

def main()
{
    var curloc, newloc, ch, s;
    
    for (s = 0; s < 4; ++s) {
        screen(s);
        print #actors[s].loc.description;
    }
    
    while (1) {
        for (s = 0; s < 4; ++s) {
            screen(s);
            curloc = actors[s].loc;
            if ((ch = getc()) != 0 && ch != '\n') {
                if (ch == 'n')
                    newloc = getp(curloc, north);
                else if (ch == 's')
                    newloc = getp(curloc, south);
                else if (ch == 'e')
                    newloc = getp(curloc, east);
                else if (ch == 'w')
                    newloc = getp(curloc, west);
                else if (ch == 'l') {
                    print #curloc.description;
                    newloc = curloc;
                }
                else
                    newloc = -1;
            
                if (newloc == -1)
                    print "Huh?";
                else if (newloc == 0)
                    print "You can't go that way.";
                else if (newloc != curloc) {
                    actors[s].loc = newloc;
                    print #newloc.description;
                }
            }
        }
    }
}
